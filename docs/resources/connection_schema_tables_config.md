---
page_title: "Resource: fivetran_connection_schema_tables_config"
subcategory: "Preview"
---

# Resource: fivetran_connection_schema_tables_config

~> **Preview** This resource is in preview. Its behavior and interface may change in future releases without prior notice.

Manages table-level settings for a specific schema within a Fivetran connection: which tables are enabled or disabled, and per-table sync modes.

The list you choose defines the complete desired state for all tables in the schema:

- **`disabled_tables`** — listed tables are **disabled**, all other tables are **enabled**
- **`enabled_tables`** — listed tables are **enabled**, all other tables are **disabled**

If a new table appears upstream, the resource will automatically apply the correct state based on which list you configured. The `sync_mode` map only tracks tables explicitly listed in it; unlisted tables' sync modes are not managed.

## Example Usage

### Disable specific tables (all others enabled)

```hcl
resource "fivetran_connection_schema_tables_config" "public" {
  connection_id   = fivetran_connector.my_connector.id
  schema_name     = "public"
  disabled_tables = ["legacy_orders", "tmp_data"]
  sync_mode = {
    "users"  = "HISTORY"
    "orders" = "SOFT_DELETE"
  }
}
```

### Enable specific tables (all others disabled)

```hcl
resource "fivetran_connection_schema_tables_config" "public" {
  connection_id  = fivetran_connector.my_connector.id
  schema_name    = "public"
  enabled_tables = ["users", "orders"]
}
```

### Using for_each for multiple schemas

```hcl
locals {
  schema_configs = {
    public    = { disabled_tables = ["tmp_data"] }
    analytics = { disabled_tables = ["raw_events"] }
    reporting = { disabled_tables = ["debug_log"] }
  }
}

resource "fivetran_connection_schema_tables_config" "schema" {
  for_each = local.schema_configs

  connection_id   = fivetran_connector.my_connector.id
  schema_name     = each.key
  disabled_tables = each.value.disabled_tables

  depends_on = [fivetran_connection_schemas_config.config]
}
```

-> **NOTE:** The `disabled_tables` and `enabled_tables` attributes conflict with each other — use one or the other.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `connection_id` (String) The unique identifier for the connection within the Fivetran system.
- `schema_name` (String) The name of the schema within the connection.

### Optional

- `disabled_tables` (List of String) Set of table names to disable. Use when the connection's schema_change_handling is ALLOW_ALL.
- `enabled_tables` (List of String) Set of table names to enable. Use when the connection's schema_change_handling is BLOCK_ALL or ALLOW_COLUMNS.
- `sync_mode` (Map of String) Map of table name to sync mode. Allowed values: LIVE, SOFT_DELETE, HISTORY.

### Read-Only

- `id` (String) The unique resource identifier ({connection_id}:{schema_name}).

## Import

Import using `connection_id:schema_name`:

```shell
terraform import fivetran_connection_schema_tables_config.public my_connection_id:public
```
